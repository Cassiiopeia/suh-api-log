plugins {
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java-library'
    id 'maven-publish'
}

group = 'me.suhsaechan'
version = project.findProperty('version') ?: '1.0.3'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    // 핵심 의존성
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'
    implementation 'org.jsoup:jsoup:1.16.1'

    // Spring 관련 의존성 - 개발환경 only
    compileOnly 'org.springframework:spring-context:6.0.11'
    compileOnly 'org.springframework.boot:spring-boot-autoconfigure:3.1.3'

    // Swagger/SpringDoc 의존성 : 개발환경 only
    compileOnly 'org.springdoc:springdoc-openapi-ui:2.1.0'

    // 테스트 의존성
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.1.3'
    testImplementation 'org.springdoc:springdoc-openapi-ui:2.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
}

test {
    useJUnitPlatform()
}

// 일반 JAR 생성
bootJar { enabled = false }
jar     { enabled = true }

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name        = 'suh-logger'
                description = '서새찬 로깅 라이브러리'
                url         = 'https://github.com/Cassiiopeia/suh-logger/'
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            name = 'nexus'
            url  = uri(
                    version.endsWith('SNAPSHOT')
                            ? "http://suh-project.synology.me:9999/repository/maven-snapshots/"
                            : "http://suh-project.synology.me:9999/repository/maven-releases/"
            )
            allowInsecureProtocol = true
            credentials {
                username = project.findProperty('nexusUsername')
                password = project.findProperty('nexusPassword')
            }
        }
    }
}
