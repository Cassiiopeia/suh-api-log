plugins {
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'java-library'
    id 'maven-publish'
}

group = 'kr.suhsaechan'
version = project.findProperty('version') ?: '1.0.4' 
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

configurations {
    // SLF4J 및 기타 로깅 관련 의존성 제외
    all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    all*.exclude group: 'ch.qos.logback', module: 'logback-classic'
    all*.exclude group: 'org.slf4j', module: 'slf4j-api'
    all*.exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    all*.exclude group: 'org.slf4j', module: 'jul-to-slf4j'
}

dependencies {
    // Spring
    compileOnly('org.springframework.boot:spring-boot-autoconfigure') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    compileOnly('org.springframework:spring-context') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }

    // API 문서화
    implementation('org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.4') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    annotationProcessor 'org.springframework.boot:spring-boot-autoconfigure-processor'

    // Jackson 의존성
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    implementation 'org.jsoup:jsoup:1.18.1'

    api 'org.reflections:reflections:0.10.2'

    // 테스트 의존성
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    testImplementation('org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.4') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

bootJar { enabled = false }
jar { enabled = true }

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'suh-api-log'
                description = '서새찬 API 로깅 라이브러리'
                url = 'https://github.com/Cassiiopeia/suh-api-log/'
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            name = 'nexus'
            url = uri(
                    version.endsWith('SNAPSHOT')
                            ? "https://nexus.suhsaechan.kr/repository/maven-snapshots/"
                            : "https://nexus.suhsaechan.kr/repository/maven-releases/"
            )
            allowInsecureProtocol = true
            credentials {
                username = project.findProperty('nexusUsername')
                password = project.findProperty('nexusPassword')
            }
        }
    }
}